#pragma kernel CSMain

// 書き出し先のテクスチャ
RWTexture2D<float4> Result;

// 解像度
float2 Resolution;

// 現在時間
float Time;

// マウス位置
float2 Mouse;

// 円周率
#define PI 3.1415926535 

// 8 * 8のスレッドで回す
[numthreads(8, 8, 1)]
void CSMain (uint3 dtid  : SV_DispatchThreadID)
{
	// スクリーン座標を計算
	float2 pos = (dtid.xy / Resolution.xy - 0.5);
	
	// マウス位置を加算する
	pos += (Mouse / Resolution.xy - 0.5);
	
	// アスペクト比を考慮する
	pos.x /= Resolution.y / Resolution.x;
	
	// 色を計算
	
	float4 color = float4(0, 0, 0, 1);
	for(int i = 0; i < 6; i++)
	{
		// 現在時間とイテレーターから、円周上の位置を計算
		float t = (Time * 0.1f + i / 6.0) * (PI * 2);
		
		// 原点からの距離を計算
		float len = sin(t * 3) * 0.25 + 0.5;
		
		// tとlenを元に、円の座標を決める
		float2 objPos = float2(cos(t), sin(t)) * len;
		
		// 半径0.2の円をobjPos座標に描く
		color.rgb += 0.001 / (abs(length(objPos - pos) - 0.2));
	}
	
	// テクスチャに色を書き込む
    Result[dtid.xy] = color;
}